plugins {
    id 'maven-publish'
}

allprojects {
    group = providers.gradleProperty('GROUP').get()
    version = providers.environmentVariable('VERSION')
            .orElse(providers.gradleProperty('VERSION_NAME'))
            .get()
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'maven-publish'

    // Aplicar configuración de publicación a todos los submódulos
    publishing {
        publications {
            maven(MavenPublication) {
                afterEvaluate {
                    from components.java
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ing-sist/printscript")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR") ?: ""
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN") ?: ""
                }
            }
        }
    }
}
